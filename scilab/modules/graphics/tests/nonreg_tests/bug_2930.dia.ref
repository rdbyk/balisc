curFig             = scf(100001);
clf(curFig,"reset");
show_window();
id=curFig.figure_id;
tb=toolbar(id);
toolbar(id,"off");
n=60;
U = linspace(0,3*%pi,n);
v = linspace(0,2*%pi,20);
u = U(1:20);
X = (cos(u).*u)'*(1+cos(v)/2);
Y = (u/2)'*sin(v);
Z = (sin(u).*u)'*(1+cos(v)/2);
[xx,yy,zz]=nf3d(X,Y,Z);
plot3d(xx,yy,zz)
e = gce();
data=e.data;
title("shell","fontsize",3)
curAxe=gca();
curAxe.data_bounds=[-15 -5 -10; 10  5  12];
curAxe.rotation_angles=[152 62];
K=20:4:n;
realtimeinit(0.1);
for k=2:size(K,"*")
    drawlater();
    realtime(k);
    //compute more facets
    u=U(K(k-1):K(k));
    X= (cos(u).*u)'*(1+cos(v)/2);
    Y= (u/2)'*sin(v);
    Z= (sin(u).*u)'*(1+cos(v)/2);
    [xx,yy,zz]=nf3d(X,Y,Z);//build facets
    //append new facets to the data data structure
    data.x=[data.x xx];
    data.y=[data.y yy];
    data.z=[data.z zz];
    e.data=data;// update the Fac3d entity
    drawnow();
end
toolbar(id,tb);
